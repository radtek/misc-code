Attribute VB_Name = "Resources"
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' John Robbins
' Microsoft Systems Journal, December 1997 - Bugslayer Column!
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Option Explicit

' This is similar to the stock LoadResStrings generated by the
'  application wizard version.  The main difference is that I
'  added MUCH better error handling to it so that you could
'  tell when a resource did not load!
' This is a whole lot better than just having the item you are
'  trying to internationalize just come up as some goofy number.
Sub LoadFormResStrings(frm As Form)

' BUG BUG
'  This needs to be tested more!

    On Error GoTo LoadResStrings_Error

    Dim ctl As Control
    Dim obj As Object
    Dim fnt As Object
    Dim sCtlType As String
    ' All items are converted into this variable.  In the
    '  error handling, this makes it easy to know which one
    '  failed.  I also made it a long because it makes it faster
    '  on both Intel and ALPHA machines.
    Dim nVal As Long


    ' Set the form's caption.
    nVal = CLng(frm.Tag)
    frm.Caption = LoadResString(nVal)

    On Error Resume Next
    ' Set the font.
    Set fnt = frm.Font
    ' WHERE DO THESE MAGIC VALUES COME FROM?
    nVal = 20
    fnt.Name = LoadResString(nVal)
    nVal = 21
    fnt.Size = CInt(LoadResString(nVal))
    On Error GoTo LoadResStrings_Error


    ' Set the controls captions using the caption property for
    '  menu items and the Tag property for all other controls
    For Each ctl In frm.Controls

        On Error Resume Next
        Set ctl.Font = fnt
        On Error GoTo LoadResStrings_Error

        sCtlType = TypeName(ctl)
        If sCtlType = "Label" Then
            If ("" <> ctl.Tag) Then
                nVal = CLng(ctl.Tag)
                ctl.Caption = LoadResString(nVal)
            End If
        ElseIf sCtlType = "Menu" Then
' BUG BUG
' Why are menu's treated differently?  They have TAG fields?
            ' If this is a sep. bar, then don't mess with it.
            If ("-" <> ctl.Caption) Then
                nVal = CLng(ctl.Caption)
                ctl.Caption = LoadResString(nVal)
            End If
        ElseIf sCtlType = "TabStrip" Then
            For Each obj In ctl.Tabs
                nVal = CLng(obj.Tag)
                obj.Caption = LoadResString(nVal)
                nVal = CLng(obj.ToolTipText)
                obj.ToolTipText = LoadResString(nVal)
            Next
        ElseIf sCtlType = "Toolbar" Then
            For Each obj In ctl.Buttons
                ' Only load those with values.
                If ("" <> obj.ToolTipText) Then
                    nVal = CLng(obj.ToolTipText)
                    obj.ToolTipText = LoadResString(nVal)
                End If
            Next
        ElseIf sCtlType = "ListView" Then
            For Each obj In ctl.ColumnHeaders
                nVal = CLng(obj.Tag)
                obj.Text = LoadResString(nVal)
            Next
        Else
            nVal = 0
            If ("" <> ctl.Tag) Then
                nVal = Val(ctl.Tag)
                If nVal > 0 Then
                    ctl.Caption = LoadResString(nVal)
                End If
            End If
            nVal = 0
            If ("" <> ctl.Tag) Then
                nVal = Val(ctl.ToolTipText)
                If nVal > 0 Then
                    ctl.ToolTipText = LoadResString(nVal)
                End If
            End If
        End If
    Next

    Exit Sub

LoadResStrings_Error:

    Debug.Print "Resource ID " + CStr(nVal) + " failed to load!"

    Resume Next

End Sub


